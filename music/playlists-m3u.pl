#!/usr/bin/perl
# Create M3U/M3U8 playlists from a MP3 tree.
use warnings;
use strict;
use File::Find;
#use Cwd;

my $script = $0;  $script =~ s,.*[/\\],,;

sub write_m3u ( $$@ ) {
  my ($file, $title, @list) = @_;
  open my $FILE, '>', $file or die "open(>$file): $!";
  print $FILE "## generated by $script\n";
  # FIXME: this will write utf8; we need to convert to latin1 ?!?
  print $FILE "$_\n" for @list;
}

sub write_wpl ( $$@ ) {
  my ($file, $title, @list) = @_;
  my $count = scalar @list;
  my $text = <<"EndOfHeader";
<?wpl version="1.0"?>
<smil>
  <head>
    <meta name="Generator" content="$script"/>
    <meta name="ItemCount" content="$count"/>
    <title>$title</title>
  </head>
  <body><seq>
EndOfHeader
  for my $item (@list) {
    $item =~ s/&/&amp;/g;
    $text .= qq[    <media src="$item"/>\n];
  }
  $text .= <<"EndOfFooter";
  </seq></body>
</smil>
EndOfFooter
  $text =~ s/\r?//g;  $text =~ s/\n/\r\n/g;
  open my $FILE, '>', $file or die "open(>$file): $!";
  print $FILE $text;
}

sub process_dir {
  -d $_ or return;
  /^\.\.?$/ and return;

  my $dir = $_;
  chdir $dir or die "chdir($_): $!";

  my @mp3s = glob '*.mp3';
  if (@mp3s > 1) {
    my @m3us = glob '00*.m3u';
    if (@m3us) {
      print "SKIP  $File::Find::name\n";
    } else {
      print "write $File::Find::name\n";
      write_m3u '00_playlist.m3u', $dir, sort @mp3s;
    }
  } elsif (@mp3s == 1) {
    print "SKIP  $File::Find::name\n";
  }

  chdir '..' or die "chdir(..): $!";
}

find \&process_dir, '.';
