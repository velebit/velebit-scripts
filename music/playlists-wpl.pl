#!/usr/bin/perl
# Create Windows Media Player playlists from a MP3/M4A/... tree.
use warnings;
use strict;
use File::Find;
#use Cwd;

my $script = $0;  $script =~ s,.*[/\\],,;

# Overwrite/remove existing playlists?
my $force = 1;
# Generate playlists in directories with only one MP3 file or subdirectory?
my $even_single = 1;

my @audio_exts = qw( mp3 m4a );
my $audio_exts_re = '\.(?i:' . join('|', map "\Q$_", @audio_exts) . ')';

sub write_m3u ( $$@ ) {
  my ($file, $title, @list) = @_;
  open my $FILE, '>', $file or die "open(>$file): $!";
  print $FILE "## generated by $script\n";
  # FIXME: this will write utf8; we need to convert to latin1 ?!?
  # FIXME: can M3U playlists contain non-MP3 files?
  print $FILE "$_\n" for @list;
}

sub write_wpl ( $$@ ) {
  my ($file, $title, @list) = @_;
  my $count = scalar @list;
  my $text = <<"EndOfHeader";
<?wpl version="1.0"?>
<smil>
  <head>
    <meta name="Generator" content="$script"/>
    <meta name="ItemCount" content="$count"/>
    <title>$title</title>
  </head>
  <body><seq>
EndOfHeader
  for my $item (@list) {
    $item =~ s,/,\\,g;
    $item =~ s,&,&amp;,g;
    $text .= qq[    <media src="$item"/>\n];
  }
  $text .= <<"EndOfFooter";
  </seq></body>
</smil>
EndOfFooter
  $text =~ s/\r?//g;  $text =~ s/\n/\r\n/g;
  open my $FILE, '>', $file or die "open(>$file): $!";
  print $FILE $text;
}

my (%files_deep, %files_here, %children, %files_exts);
sub process_files {
  my $base = $File::Find::name;
  $base =~ s/($audio_exts_re)$// or return;
  my $ext  = $1;
  -f $_    or return;

  my @dir = split m:/:, $base;
  my @file = pop @dir;

  if (exists $files_exts{join '/', @dir, @file}) {
    # A file with the same name but a different extension was already found.
    $files_exts{join '/', @dir, @file}{$ext}++;
    return;
  }

  while (@dir) {
    my $dir  = join '/', @dir;
    my $file = join '/', @file;
    push @{$files_deep{$dir}}, $file;
    push @{$files_here{$dir}}, $file if @file == 1;
    $children{$dir}{$file[0]}++;
    $files_exts{join '/', @dir, @file}{$ext}++;
    $dir[-1] eq 'christmas' and last;
    $dir[-1] eq 'classical' and last;
    $dir[-1] eq 'kids'      and last;
    unshift @file, pop @dir;
  }
}
sub base_with_ext ( $$ ) {
  my ($dir, $base) = @_;
  exists $files_exts{"$dir/$base"}
    or die "full name not known for '$dir'/'$base'";
  my $ext_hash = $files_exts{"$dir/$base"};
  # find the first entry in @audio_exts that matches
  for my $e (@audio_exts) {
    exists $ext_hash->{".$e"} and return "$base.$e";
  }
  die "extensions not matched for '$dir'/'$base' (@{[keys %$ext_hash]})";
}

find +{ preprocess => sub { sort @_; }, wanted => \&process_files }, '.';

for my $dir (sort keys %files_deep) {
  my $wpl = "$dir/00_playlist.wpl";

  # A playlist is expected only if
  # - $even_single is true and there are some MP3s or subdirs here, OR
  # - $even_single is false and there are at least 2 MP3s or subdirs here
  my $count = ( ($files_here{$dir} ? @{$files_here{$dir}} : 0) +
		($children{$dir} ? keys %{$children{$dir}} : 0) );
  my $limit = ($even_single ? 1 : 2);

  if ($count < $limit) {
    if (! -e $wpl) {
      print "SKIP  $dir\n";
    } elsif (!$force) {
      print "LEAVE $dir\n";
    } else {
      print "rm    $dir\n";
      unlink $wpl or warn "unlink($wpl): $!";
    }
    next;
  }

  if (-e $wpl && !$force) {
    print "KEEP  $dir\n";
  } else {
    print "write $dir\n";
    my $name = $dir;  $name =~ s,^.*/,,;
    write_wpl $wpl, $name, map base_with_ext($dir, $_), @{$files_deep{$dir}};
  }
}
